//CM1 % calculation - shouldn't be used average of CM1% values from main table

CM1%_Calc = SUM('GM-report-H1_full_unpivot'[CM1])/SUM('GM-report-H1_full_unpivot'[Turnover])


//CM2 % calculation - shouldn't be used average of CM2% values from main table

CM2%_Calc = SUM('GM-report-H1_full_unpivot'[CM2])/SUM('GM-report-H1_full_unpivot'[Turnover])



//Calculate product's labour cost %

Labour%_Calc = 
VAR TotalLabour = SUMX('GM-report-H1_full_unpivot', [Sales qty - prev. month] * [Labour costs EUR])
VAR TotalTurnover = SUM('GM-report-H1_full_unpivot'[Turnover])

RETURN
DIVIDE(
    TotalLabour, 
    TotalTurnover, 
    0
)


//Calculate product's machine cost %

Machine%_Calc = 
VAR TotalMachine = SUMX('GM-report-H1_full_unpivot', [Sales qty - prev. month] * [Machine costs EUR])
VAR TotalSetup = SUMX('GM-report-H1_full_unpivot', [Sales qty - prev. month] * [Setup costs EUR])
VAR TotalTurnover = SUM('GM-report-H1_full_unpivot'[Turnover])

RETURN
DIVIDE(
    TotalMachine + TotalSetup, 
    TotalTurnover, 
    0
)


//Define MaterialCost absolute differences between Months with values

MaterialCost_Diff = 
VAR LatestPeriod =
    MAXX(
        FILTER(
            VALUES('GM-report-H1_full_unpivot'),
            NOT( ISBLANK( 'GM-report-H1_full_unpivot'[Material costs EUR] ) )
        ),
        'GM-report-H1_full_unpivot'[Calculation period]
    )

VAR EarliestPeriod =
    MINX(
        FILTER(
            VALUES('GM-report-H1_full_unpivot'),
            NOT( ISBLANK( 'GM-report-H1_full_unpivot'[Material costs EUR] ) )
        ),
        'GM-report-H1_full_unpivot'[Calculation period]
    )

VAR LatestCost =
    CALCULATE(
        SUM('GM-report-H1_full_unpivot'[Material costs EUR]),
        'GM-report-H1_full_unpivot'[Calculation period] = LatestPeriod
    )
VAR EarliestCost =
    CALCULATE(
        SUM('GM-report-H1_full_unpivot'[Material costs EUR]),
        'GM-report-H1_full_unpivot'[Calculation period] = EarliestPeriod
    )
RETURN
LatestCost - EarliestCost


//Define MaterialCost differences in % between Months with values

MaterialCost_Diff% = 
VAR LatestPeriod =
    MAXX(
        FILTER(
            VALUES('GM-report-H1_full_unpivot'),
            NOT( ISBLANK( 'GM-report-H1_full_unpivot'[Material costs EUR] ) )
        ),
        'GM-report-H1_full_unpivot'[Calculation period]
    )

VAR EarliestPeriod =
    MINX(
        FILTER(
            VALUES('GM-report-H1_full_unpivot'),
            NOT( ISBLANK( 'GM-report-H1_full_unpivot'[Material costs EUR] ) )
        ),
        'GM-report-H1_full_unpivot'[Calculation period]
    )

VAR LatestCost =
    CALCULATE(
        SUM('GM-report-H1_full_unpivot'[Material costs EUR]),
        'GM-report-H1_full_unpivot'[Calculation period] = LatestPeriod
    )
VAR EarliestCost =
    CALCULATE(
        SUM('GM-report-H1_full_unpivot'[Material costs EUR]),
        'GM-report-H1_full_unpivot'[Calculation period] = EarliestPeriod
    )
RETURN
(LatestCost/EarliestCost)-1


// Main KAM Matrix calculated to specify monthly averages and define unique formats by KPIs

FormattedKPI_with_Average = 
IF(
    HASONEVALUE('GM-report-H1_full_KPI'[Month]),
    [FormattedKPI],
    
    VAR ActualKPI = SELECTEDVALUE('GM-report-H1_full_KPI'[KPIs])
    RETURN
    SWITCH(
        ActualKPI,
        "CM1 %", SUM('GM-report-H1_full_unpivot'[CM1]) / SUM('GM-report-H1_full_unpivot'[Turnover]),
        "CM2 %", [CM2%_Calc],
        "EBIT %", [Ebit%_Calc],
        "Material ratio %", [Material%_Calc],
        
        AVERAGEX(
            VALUES('GM-report-H1_full_KPI'[Month]),
            CALCULATE(SUM('GM-report-H1_full_KPI'[Amount]))
        )
    )
)

SWITCH(SELECTEDVALUE('GM-report-H1_full_KPI'[KPIs]),
    "CM1 %", "0.00%",
    "CM2 %", "0.00%",
    "EBIT %", "0.00%",
    "Material ratio (incl. 3rd party) in %", "0.00%",
    "Budget Qty", "#,##0",
    "Sales qty - prev. month", "#,##0",
    "#,##0.00 â‚¬")

